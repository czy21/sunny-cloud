plugins {
    id 'org.springframework.boot' apply false
}
def bomName = "sunny-framework-bom"

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.sunny'
    version = '3.0.0-SNAPSHOT'

    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs.add("-parameters")
    }

    dependencyManagement {
        generatedPomCustomization {
            enabled = false
        }
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAliVersion}"
        }
        dependencies {
            dependency "commons-io:commons-io:2.11.0"
            dependency "org.apache.commons:commons-collections4:4.4"
            dependency "commons-validator:commons-validator:1.7"

            dependency 'com.clickhouse:clickhouse-jdbc:0.3.2'
            dependency 'com.github.xiaoymin:knife4j-openapi3-jakarta-spring-boot-starter:4.3.0'
            dependency 'com.github.xiaoymin:knife4j-openapi3-webflux-jakarta-spring-boot-starter:4.3.0'
            dependency "io.swagger.core.v3:swagger-annotations-jakarta:2.2.8"
            dependency "org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3"
            dependency "com.github.pagehelper:pagehelper:5.3.3"
            dependency 'com.github.pagehelper:pagehelper-spring-boot-starter:1.4.7'

            dependency "org.redisson:redisson-spring-boot-starter:3.20.0"

            dependency "io.minio:minio:8.5.2"
            dependency "com.xuxueli:xxl-job-core:2.4.0"
            dependency "org.mapstruct:mapstruct:1.5.3.Final"
            dependency "org.mapstruct:mapstruct-processor:1.5.3.Final"
            dependency "org.projectlombok:lombok-mapstruct-binding:0.2.0"

            dependency "com.squareup.okhttp3:okhttp:4.10.0"

            dependency "io.micrometer:micrometer-registry-prometheus:1.10.4"
            dependency "io.github.mweirauch:micrometer-jvm-extras:0.2.2"

            dependency "com.alibaba:easyexcel:3.2.0"
            dependency "org.apache.pulsar:pulsar-client:2.11.0"

            dependency "net.logstash.logback:logstash-logback-encoder:7.4"
            dependency 'org.codehaus.janino:janino:3.1.10'
        }
    }

    test {
        useJUnitPlatform()
    }

    jar {
        into("META-INF/maven/${project.group}/${project.name}") {
            from { generatePomFileForMavenJavaPublication }
            rename "pom-default.xml", "pom.xml"
        }

        afterEvaluate {
            manifest {
                attributes 'Implementation-Title': archiveBaseName
                attributes 'Implementation-Version': archiveVersion
                attributes 'Build-Gradle': gradle.gradleVersion
                attributes 'Build-OS': System.getProperty("os.name")
                attributes 'Build-By': System.getProperty("user.name")
                attributes 'Build-Jdk-Spec': System.getProperty("java.version")
                attributes 'Build-SourceCompatibility': sourceCompatibility
                attributes 'Build-TargetCompatibility': targetCompatibility
                attributes 'Build-Time': LocalDateTime.now().format("yyyy-MM-dd HH:mm:ss")
            }
        }
    }

    java {
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }

    List<String> applyPlugins = []
    if (bomName != name) {
        applyPlugins.add("spring.build")
    }
    logger.info("${project.name} apply plugins: ${applyPlugins}")
    applyPlugins.forEach { t -> project.plugins.apply(t) }
}