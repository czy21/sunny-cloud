plugins {
    id 'org.springframework.boot' apply false
}
def bomName = "sunny-framework-bom"

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.sunny'
    version = '3.0.0-SNAPSHOT'

    dependencyManagement {
        generatedPomCustomization {
            enabled = false
        }
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAliVersion}"
            mavenBom "org.springframework.statemachine:spring-statemachine-bom:${springStatemachineVersion}"
        }
        dependencies {
            dependency "commons-io:commons-io:2.16.1"
            dependency "org.apache.commons:commons-collections4:4.4"
            dependency "commons-validator:commons-validator:1.9.0"

            dependency 'com.clickhouse:clickhouse-jdbc:0.6.2'
            dependency 'com.github.xiaoymin:knife4j-openapi3-jakarta-spring-boot-starter:4.3.0'
            dependency 'com.github.xiaoymin:knife4j-openapi3-webflux-jakarta-spring-boot-starter:4.3.0'
            dependency "io.swagger.core.v3:swagger-annotations-jakarta:2.2.22"
            dependency "org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3"
            dependency "com.github.pagehelper:pagehelper:6.1.0"
            dependency 'com.github.pagehelper:pagehelper-spring-boot-starter:2.1.0'

            dependency "org.redisson:redisson-spring-boot-starter:3.32.0"
            dependency "com.esotericsoftware:kryo:5.6.0"
            
            dependency "io.minio:minio:8.5.11"
            dependency "com.xuxueli:xxl-job-core:2.4.1"
            dependency "org.mapstruct:mapstruct:1.5.3.Final"
            dependency "org.mapstruct:mapstruct-processor:1.5.3.Final"
            dependency "org.projectlombok:lombok-mapstruct-binding:0.2.0"

            dependency "com.squareup.okhttp3:okhttp:4.12.0"

            dependency "io.micrometer:micrometer-registry-prometheus:1.13.2"
            dependency "io.github.mweirauch:micrometer-jvm-extras:0.2.2"

            dependency "com.alibaba:easyexcel:4.0.1"
            dependency "org.apache.pulsar:pulsar-client:3.3.0"

            dependency "net.logstash.logback:logstash-logback-encoder:7.4"
            dependency 'org.codehaus.janino:janino:3.1.12'
        }
    }

    jar {
        into("META-INF/maven/${project.group}/${project.name}") {
            from { generatePomFileForMavenJavaPublication }
            rename "pom-default.xml", "pom.xml"
        }
    }

    apply plugin: 'common.build'
    apply plugin: 'publish.build'

    List<String> applyPlugins = []
    if (bomName != name) {
        applyPlugins.add("spring.build")
    }
    logger.info("${project.name} apply plugins: ${applyPlugins}")
    applyPlugins.forEach { t -> project.plugins.apply(t) }
}